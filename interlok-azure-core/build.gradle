ext {
  componentName='Interlok Azure'
  delombokTargetDir = new File("${project.projectDir}/src/main/generated")
  jacksonVersion = "2.12.1"
  nettyVersion='4.1.59.Final'
}

dependencies {

  compile ("com.microsoft.azure:msal4j:1.8.1")
  compile ("com.microsoft.graph:microsoft-graph:2.5.0")
  compile ("com.azure:azure-identity:1.2.1") {
    // Exclude KeePassJava2 dependencies, because we don't care
    // If people want it, then can include it themselves.
    exclude group: "org.linguafranca.pwdb", module: "KeePassJava2"
    exclude group: "org.simpleframework", module: "simple-xml"
    exclude group: "io.netty"
  }
  compile ("com.azure:azure-storage-file-datalake:12.3.0") {
    exclude group: "io.netty"
  }

  implementation ("io.netty:netty-handler:$nettyVersion")
  implementation ("io.netty:netty-handler-proxy:$nettyVersion")
  implementation ("io.netty:netty-transport-native-epoll:$nettyVersion")
  implementation ("io.netty:netty-transport-native-unix-common:$nettyVersion")
  implementation ("io.netty:netty-transport-native-kqueue:$nettyVersion")
  implementation ("io.netty:netty-codec:$nettyVersion")
  implementation ("io.netty:netty-codec-http2:$nettyVersion")
  implementation ("io.netty:netty-codec-http:$nettyVersion")

  compile ("org.codehaus.staxmate:stax2:2.1")
  compile ("com.google.guava:guava:30.1-jre")
  // azure-core via azure-identity has an explicit dependency on these jars,
  // so we need to pin the right version
  compile ("com.fasterxml.jackson.datatype:jackson-datatype-jsr310:$jacksonVersion")
  compile ("com.fasterxml.jackson.dataformat:jackson-dataformat-xml:$jacksonVersion")
  // should be pulled in by the jackson-datatype/dataformat deps.
  compile ("com.fasterxml.jackson.core:jackson-databind:$jacksonVersion")
  compile ("com.fasterxml.jackson.core:jackson-core:$jacksonVersion")

  compile ("commons-codec:commons-codec:1.15")
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

task examplesJar(type: Jar, dependsOn: test) {
    classifier = 'examples'
    from new File(buildDir, '/examples')
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.extensions.delombokTask
}


jar {
  manifest {
    attributes("Built-By": System.getProperty('user.name'),
              "Build-Jdk": System.getProperty('java.version'),
              "Implementation-Title": componentName,
              "Implementation-Version": project.version,
              "Implementation-Vendor-Id": project.group,
              "Implementation-Vendor": organizationName)
  }
}

artifacts {
    archives javadocJar
    archives examplesJar
    archives sourcesJar
}

publishing {
  publications {
    mavenJava(MavenPublication) {
      from components.java

      artifact javadocJar { classifier "javadoc" }
      artifact examplesJar { classifier "examples" }
      artifact sourcesJar { classifier "sources" }
      pom.withXml {
        asNode().appendNode("name", componentName)
        asNode().appendNode("description", "Components that interact Outlook/Exchange in Office365 ")
        def properties = asNode().appendNode("properties")
        properties.appendNode("target", "3.11.1+")
        properties.appendNode("tags", "azure,office365,outlook,exchange")
        properties.appendNode("license", "false")
        properties.appendNode("readme", "https://github.com/adaptris/interlok-azure/raw/develop/README.md")
        properties.appendNode("repository", "https://github.com/adaptris/interlok-azure")
      }
    }
  }
  repositories {
    maven {
      credentials {
        username repoUsername
        password repoPassword
      }
      url mavenPublishUrl
    }
  }
}

delombok {
  target = delombokTargetDir
}

task deleteGeneratedFiles(type: Delete) {
  delete delombokTargetDir
}

processTestResources.dependsOn copyUnitTestProperties
javadoc.dependsOn offlinePackageList
clean.dependsOn deleteGeneratedFiles
