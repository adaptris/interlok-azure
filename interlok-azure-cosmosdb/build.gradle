ext {
  componentName = "Interlok Azure/CosmosDB"
  componentDesc = "Components that interact with Azure CosmosDB"
  delombokTargetDir = new File("${project.projectDir}/src/main/generated")

  jacksonVersion = "2.18.2"
  jsonOrgVersion = "20240303"
}

dependencies {
  api ("com.microsoft.azure:azure-documentdb:2.6.5"){
    exclude group: "com.fasterxml.jackson.core", module: "jackson-databind"
    exclude group: "com.fasterxml.jackson.core", module: "jackson-core"
    exclude group: "org.json", module: "json"
  }
  
  implementation ("org.json:json:$jsonOrgVersion")
  
  // Dependency to platform/BOM
  api (platform("com.fasterxml.jackson:jackson-bom:$jacksonVersion"))
  api ("com.fasterxml.jackson.core:jackson-databind")
  api ("com.fasterxml.jackson.core:jackson-core")
  api ("commons-codec:commons-codec:1.17.1")

  api ("com.adaptris:interlok-apache-http:$interlokCoreVersion")
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

task examplesJar(type: Jar, dependsOn: test) {
    classifier = 'examples'
    from new File(buildDir, '/examples')
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.extensions.delombokTask
}


jar {
  manifest {
    attributes("Built-By": System.getProperty('user.name'),
              "Build-Jdk": System.getProperty('java.version'),
              "Implementation-Title": componentName,
              "Implementation-Version": project.version,
              "Implementation-Vendor-Id": project.group,
              "Implementation-Vendor": organizationName)
  }
}

artifacts {
    archives javadocJar
    archives examplesJar
    archives sourcesJar
}

publishing {
  publications {
    mavenJava(MavenPublication) {
      from components.java

      artifact javadocJar { classifier "javadoc" }
      artifact examplesJar { classifier "examples" }
      artifact sourcesJar { classifier "sources" }
      pom.withXml {
        asNode().appendNode("name", componentName)
        asNode().appendNode("description", componentDesc)
		asNode().appendNode("url", "https://interlok.adaptris.net/interlok-docs/#/")
        def properties = asNode().appendNode("properties")
        properties.appendNode("target", "3.9.2+")
        properties.appendNode("tags", "azure,cosmosdb,cosmos")
        properties.appendNode("license", "false")
        properties.appendNode("readme", "https://github.com/adaptris/interlok-azure/raw/develop/README.md")
        properties.appendNode("repository", "https://github.com/adaptris/interlok-azure")
      }
    }
  }
  repositories {
    maven {
      credentials {
        username repoUsername
        password repoPassword
      }
      url mavenPublishUrl
    }
  }
}

delombok {
  target = delombokTargetDir
}

task deleteGeneratedFiles(type: Delete) {
  delete delombokTargetDir
}


processTestResources.dependsOn copyUnitTestProperties
javadoc.dependsOn offlinePackageList
clean.dependsOn deleteGeneratedFiles
